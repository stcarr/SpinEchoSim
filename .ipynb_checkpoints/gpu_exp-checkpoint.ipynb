{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# for non-hermitian\n",
    "\n",
    "function exp!(A::StridedMatrix{T}) where T<:BlasFloat\n",
    "    n = checksquare(A)\n",
    "    if ishermitian(A)\n",
    "        return copytri!(parent(exp(Hermitian(A))), 'U', true)\n",
    "    end\n",
    "    ilo, ihi, scale = LAPACK.gebal!('B', A)    # modifies A\n",
    "    nA   = opnorm(A, 1)\n",
    "    Inn    = Matrix{T}(I, n, n)\n",
    "    ## For sufficiently small nA, use lower order PadÃ©-Approximations\n",
    "    if (nA <= 2.1)\n",
    "        if nA > 0.95\n",
    "            C = T[17643225600.,8821612800.,2075673600.,302702400.,\n",
    "                     30270240.,   2162160.,    110880.,     3960.,\n",
    "                           90.,         1.]\n",
    "        elseif nA > 0.25\n",
    "            C = T[17297280.,8648640.,1995840.,277200.,\n",
    "                     25200.,   1512.,     56.,     1.]\n",
    "        elseif nA > 0.015\n",
    "            C = T[30240.,15120.,3360.,\n",
    "                    420.,   30.,   1.]\n",
    "        else\n",
    "            C = T[120.,60.,12.,1.]\n",
    "        end\n",
    "        A2 = A * A\n",
    "        P  = copy(Inn)\n",
    "        U  = C[2] * P\n",
    "        V  = C[1] * P\n",
    "        for k in 1:(div(size(C, 1), 2) - 1)\n",
    "            k2 = 2 * k\n",
    "            P *= A2\n",
    "            U += C[k2 + 2] * P\n",
    "            V += C[k2 + 1] * P\n",
    "        end\n",
    "        U = A * U\n",
    "        X = V + U\n",
    "        LAPACK.gesv!(V-U, X)\n",
    "    else\n",
    "        s  = log2(nA/5.4)               # power of 2 later reversed by squaring\n",
    "        if s > 0\n",
    "            si = ceil(Int,s)\n",
    "            A /= convert(T,2^si)\n",
    "        end\n",
    "        CC = T[64764752532480000.,32382376266240000.,7771770303897600.,\n",
    "                1187353796428800.,  129060195264000.,  10559470521600.,\n",
    "                    670442572800.,      33522128640.,      1323241920.,\n",
    "                        40840800.,           960960.,           16380.,\n",
    "                             182.,                1.]\n",
    "        A2 = A * A\n",
    "        A4 = A2 * A2\n",
    "        A6 = A2 * A4\n",
    "        U  = A * (A6 * (CC[14].*A6 .+ CC[12].*A4 .+ CC[10].*A2) .+\n",
    "                  CC[8].*A6 .+ CC[6].*A4 .+ CC[4].*A2 .+ CC[2].*Inn)\n",
    "        V  = A6 * (CC[13].*A6 .+ CC[11].*A4 .+ CC[9].*A2) .+\n",
    "                   CC[7].*A6 .+ CC[5].*A4 .+ CC[3].*A2 .+ CC[1].*Inn\n",
    "\n",
    "        X = V + U\n",
    "        LAPACK.gesv!(V-U, X)\n",
    "\n",
    "        if s > 0            # squaring to reverse dividing by power of 2\n",
    "            for t=1:si; X *= X end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Undo the balancing\n",
    "    for j = ilo:ihi\n",
    "        scj = scale[j]\n",
    "        for i = 1:n\n",
    "            X[j,i] *= scj\n",
    "        end\n",
    "        for i = 1:n\n",
    "            X[i,j] /= scj\n",
    "        end\n",
    "    end\n",
    "\n",
    "    if ilo > 1       # apply lower permutations in reverse order\n",
    "        for j in (ilo-1):-1:1; rcswap!(j, Int(scale[j]), X) end\n",
    "    end\n",
    "    if ihi < n       # apply upper permutations in forward order\n",
    "        for j in (ihi+1):n;    rcswap!(j, Int(scale[j]), X) end\n",
    "    end\n",
    "    X\n",
    "end\n",
    "\n",
    "## Swap rows i and j and columns i and j in X\n",
    "function rcswap!(i::Integer, j::Integer, X::StridedMatrix{<:Number})\n",
    "    for k = 1:size(X,1)\n",
    "        X[k,i], X[k,j] = X[k,j], X[k,i]\n",
    "    end\n",
    "    for k = 1:size(X,2)\n",
    "        X[i,k], X[j,k] = X[j,k], X[i,k]\n",
    "    end\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.4.2",
   "language": "julia",
   "name": "julia-1.4"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.4.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
